plugins {
    id 'org.jetbrains.intellij' version '1.9.0'
    id 'org.jetbrains.kotlin.jvm' version '1.5.10'
    id 'org.jetbrains.kotlin.kapt' version '1.5.10'
    id 'java'
}

group 'com.project.iosephknecht'
version 'v1.0.0-alpha01-221.6008.13'

def idePathKey = 'ANDROID_STUDIO_PATH'
def platformPrefix = 'AndroidStudio'
def localProperties = new Properties()

localProperties.load(new FileInputStream(rootProject.file("local.properties")))

repositories {
    mavenCentral()
    google()
}

ext {
    kotlin_version = '1.5.10'
    rx_java_version = '3.1.5'
    rx_swing_version = '3.1.1'
    dagger_version = '2.43.2'
    junit_version = '5.9.0'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "io.reactivex.rxjava3:rxjava:$rx_java_version"
    implementation "com.github.akarnokd:rxjava3-swing:$rx_swing_version"

    implementation "com.google.dagger:dagger:$dagger_version"

    runtimeOnly "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"

    kapt "com.google.dagger:dagger-compiler:$dagger_version"
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version = '221.6008.13'
    type = 'IC'
    plugins = ['android']
}

buildSearchableOptions {
    enabled = false
}
patchPluginXml {
    changeNotes.set("""
      - Raising the intellij version to 221.6008.13. <br>
      """
    )
}
runIde {
    if (localProperties.containsKey(idePathKey)) {
        systemProperty('idea.platform.prefix', platformPrefix)
        ideDir = file(localProperties.getProperty(idePathKey))
    }
}
compileKotlin {
    kotlinOptions.jvmTarget = "11"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}
test {
    useJUnitPlatform()
}